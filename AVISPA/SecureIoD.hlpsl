%%% SecureIoD
%%% 1. A -> B : {A,B,Na}_CRP
%%% 2. B -> A : {A,B,Na,Nb}_CRP
%%% 3. A -> B : {A,B,Nb,Nc}_CRP
%%% 4. A -> B : {A,B,Nb,Nc,R}_CRP

role role_A(A:agent,B:agent,CRP:symmetric_key,Hash:hash_func,SND,RCV:channel(dy))
played_by A
def=
	local
		State:nat,
		Na,Nb,Nc:text,
		MAC,MAC2,MAC34,Ca,Ra,K:message
	init
		State := 0
	transition
		1. State=0 /\ RCV(start) =|> 
		State':=2 /\ Na':=new() /\ MAC' := Hash({A.B.Na'}_CRP.Na') 
		/\ SND({A.B.Na'}_CRP.MAC')

		2. State=2 /\ RCV({A.B.Na.Nb'}_CRP.MAC2') =|> 
		State':=4 /\ Nc':=new() /\ Ca' := {Nb'.Nc'}_CRP /\ Ra':= Hash(Ca')
		/\ CRP' := (Ca'.Ra')
		/\ MAC2' := Hash({A.B.Na.Nb'}_CRP.Na.Nb')
		/\ MAC34' := Hash({A.B.Nb'.Nc'}_CRP.{A.B.Nb'.Nc'.Ra'}_CRP.Nc'.Ra')
		/\ K' := xor(Hash(Nb'),Hash(Nc'))
		/\ SND({A.B.Nb'.Nc'}_CRP.{A.B.Nb'.Nc'.Ra'}_CRP.MAC34')
		/\ secret(Nc',sec_2,{A,B}) /\ secret(Ra',sec_3,{A,B})

		/\ request(A,B,auth_1,Na)
		/\ witness(A,B,auth_2,Nb')
end role

role role_B(A:agent,B:agent,CRP:symmetric_key,Hash:hash_func,SND,RCV:channel(dy))
played_by B
def=
	local
		State:nat,
		Na,Nb,Nc:text,
		MAC,MAC2,MAC34,Ca,Ra,K:message
	init
		State := 1
	transition
		1. State=1 /\ RCV({A.B.Na'}_CRP.MAC') =|> 
		State':=3 /\ Nb' := new() 
		/\ MAC' := Hash({A.B.Na'}_CRP.Na')
		/\ MAC2' := Hash({A.B.Na'.Nb'}_CRP.Na'.Nb')
		/\ SND({A.B.Na'.Nb'}_CRP.MAC2')
		/\ secret(Nb',sec_1,{A,B})

		/\witness(B,A,auth_1,Na')

		2. State=3 /\ RCV({A.B.Nb.Nc'}_CRP.{A.B.Nb.Nc'.Ra'}_CRP.MAC34')=|> 
		State' := 5 
		/\ MAC34' := Hash({A.B.Nb.Nc'}_CRP.{A.B.Nb.Nc'.Ra'}_CRP.Nc'.Ra')
		/\ Ca' := {Nb.Nc'}_CRP /\ CRP' := (Ca'.Ra')
		/\ K' := xor(Hash(Nb),Hash(Nc')) 

		/\request(B,A,auth_2,Nb)
end role

role session(A:agent,B:agent,CRP:symmetric_key,Hash:hash_func)
def=
	local
		SND2,RCV2,SND1,RCV1:channel(dy)
	composition
		role_A(A,B,CRP,Hash,SND1,RCV1) /\ 
		role_B(A,B,CRP,Hash,SND2,RCV2)
end role

role environment()
def=
	const
		drone,groundstation:agent,
		crp:symmetric_key,
		h:hash_func,
		sec_1,sec_2,sec_3,auth_1,auth_2:protocol_id
	intruder_knowledge = {drone,groundstation,h}

	composition
		session(drone,groundstation,crp,h)
		/\ session(drone,groundstation,crp,h)
		/\ session(i,groundstation,crp,h)
		/\ session(drone,i,crp,h)
end role

goal
	secrecy_of sec_1
	secrecy_of sec_2
	secrecy_of sec_3
	authentication_on auth_1
	authentication_on auth_2
end goal

environment()
